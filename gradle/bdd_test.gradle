sourceSets {
    bddTest {
        java.srcDir 'src/bdd_test/java'
        resources.srcDir 'src/bdd_test/resources'
    }
}

dependencies {
    bddTestCompile sourceSets.main.output
    bddTestCompile sourceSets.test.output
    bddTestCompile sourceSets.fastIntegrationTest.output

    bddTestCompile configurations.compile
    bddTestCompile configurations.testCompile
    bddTestCompile configurations.fastIntegrationTestCompile

    bddTestRuntime configurations.runtime
    bddTestRuntime configurations.testRuntime
    bddTestRuntime configurations.fastIntegrationTestRuntime

    // the cucumber stuff we'll need.  Cucumber is a BDD tool.
    testCompile 'io.cucumber:cucumber-java:4.8.0'
    testCompile 'io.cucumber:cucumber-junit:4.8.0'
}

configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}
// some of the configuration for running Cucumber BDD
task cucumber (type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the BDD tests.'
    dependsOn assemble, compileTestJava, compileBddTestJava
    testClassesDirs = sourceSets.bddTest.output.classesDirs
    classpath = sourceSets.bddTest.runtimeClasspath
    doLast {
        javaexec {
            // get the path to the jar file for jacocoagent
            def jacocoAgent = zipTree(configurations.jacocoAgent.singleFile).filter { it.name == "jacocoagent.jar" }.singleFile
            jvmArgs = ["-javaagent:$jacocoAgent=destfile=$buildDir/jacoco/cucumber.exec,append=false"]

            main = "cucumber.api.cli.Main"
            classpath = configurations.cucumberRuntime +
                    sourceSets.main.output +
                    sourceSets.test.output +
                    sourceSets.bddTest.output +
                    sourceSets.fastIntegrationTest.output

            args = ['--monochrome',
                    '--plugin', 'progress',
                    '--plugin', 'json:build/bdd/file.json',
                    '--plugin', 'html:build/reports/bdd/cucumber-html-report-basic',
                    '--glue', 'com.coveros.training',
                    'src/bdd_test/resources']
        }
    }
    mustRunAfter test, fastIntegrationTest
}

check.dependsOn cucumber

