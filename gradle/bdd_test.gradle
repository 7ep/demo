sourceSets {
    bddTest {
        java.srcDir 'src/bdd_test/java'
        resources.srcDir 'src/bdd_test/resources'
    }
}

dependencies {
    bddTestImplementation configurations.fastIntegrationTestImplementation
}

// some of the configuration for running Cucumber BDD
task cucumber (type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the BDD tests.'
    dependsOn assemble, compileTestJava, compileBddTestJava
    testClassesDirs = sourceSets.bddTest.output.classesDirs
    classpath = sourceSets.bddTest.runtimeClasspath
    doLast {
        javaexec {
            // get the path to the jar file for jacocoagent
            def jacocoAgent = zipTree(configurations.jacocoAgent.singleFile).filter { it.name == "jacocoagent.jar" }.singleFile
            jvmArgs = ["-javaagent:$jacocoAgent=destfile=$buildDir/jacoco/cucumber.exec,append=false"]

            main = "io.cucumber.core.cli.Main"
            classpath = configurations.bddTestCompileClasspath +
                    sourceSets.main.output +
                    sourceSets.test.output +
                    sourceSets.bddTest.output +
                    sourceSets.fastIntegrationTest.output

            args = ['--monochrome',
                    '--plugin', 'progress',
                    '--plugin', 'json:build/bdd/file.json',
                    '--plugin', 'html:build/reports/bdd/cucumber-html-report-basic',
                    '--glue', 'com.coveros.training',
                    'src/bdd_test/resources']
        }
    }
    mustRunAfter test, fastIntegrationTest
}

check.dependsOn cucumber

